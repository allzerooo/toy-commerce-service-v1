plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.25' apply false
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25' apply false
	id 'org.springframework.boot' version '3.5.7' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

ext {
	set('springCloudVersion', "2025.0.0")
}

subprojects {
	group = 'com.toy'
	description = 'Commerce Platform'

	apply plugin: 'java'
	apply plugin: 'org.jetbrains.kotlin.jvm'
	apply plugin: 'org.jetbrains.kotlin.plugin.spring'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	repositories {
		mavenCentral()
	}

	// 공통 의존성 설정
	dependencies {
		implementation 'org.jetbrains.kotlin:kotlin-reflect'
		implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	// 공통 Spring Cloud BOM
	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	// 공통 Kotlin 설정
	kotlin {
		compilerOptions {
			freeCompilerArgs.addAll '-Xjsr305=strict'
		}
	}

	// 공통 테스트 설정
	tasks.named('test') {
		useJUnitPlatform()
	}
}


